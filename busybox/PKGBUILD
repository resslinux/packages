pkgbase='busybox'
pkgname=('busybox-doc' 'busybox')
pkgver=1.28.0.r69.g77a6678c4
pkgrel=1
pkgdesc='Size optimized toolbox of many common UNIX utilities'
url='https://busybox.net/'
arch=('x86_64')
license=('GPL2')
depends_doc=()
depends=('musl')
makedepends=('linux-headers')
source=('git://busybox.net/busybox.git'
        'config'
        'clang.patch')
sha256sums=('SKIP'
            'af9a69f3308c492d6c3cef9286af786c97cdef166cbf4b091feb09b5bac232ac'
            'e5728d8291834c5ea2f71efc5befb4a3c3987c1a6920065afc701959d432e901')

pkgver() {
  cd "${pkgbase}"
  
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/[_-]/./g'
}

prepare() {
  cd "${pkgbase}"
  
  patch -Np0 < "${srcdir}/clang.patch"
  
  cp -f "${srcdir}/config" .config
  
  #set install directory
  sed -i -e "s|CONFIG_PREFIX=.*|CONFIG_PREFIX=\"${tmpdir}\"|" .config
  
  yes "" | make oldconfig
}

build() {
  cd "$pkgbase"
  
  make
}

package() {
  package-init
  
  cd "${pkgbase}"
  
  make install
  
  install -d "${tmpdir}/var/lib/udhcpd"
  install -Dm644 "examples/udhcp/udhcpd.conf" "${tmpdir}/etc/udhcpd.conf"
  install -Dm755 "examples/udhcp/simple.script" "${tmpdir}/usr/share/udhcpc/default.script"
  
  install -Dm644 "docs/busybox.1" "${tmpdir}/usr/share/man/man1"
}

package_busybox-doc() {
  package-doc
}

package_busybox() {
  groups=('base')
  
  package-base
}
