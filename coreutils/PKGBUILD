pkgbase='coreutils'
pkgname=('coreutils-doc' 'join' 'coreutils')
pkgver=8.29.r13.g0a85449dd
pkgrel=1
pkgdesc='The basic file, shell and text manipulation utilities'
url='https://www.gnu.org/software/coreutils/coreutils.html'
arch=('x86_64')
license=('GPL3')
depends_doc=()
depends=('musl' 'libattr')
makedepends=('make' 'clang')
source=('git://git.savannah.gnu.org/coreutils.git'
        'git://git.savannah.gnu.org/gnulib.git')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "${pkgbase}"
  
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgbase}"
  
  CFLAGS=${CFLAGS//'-flto=thin'/'-flto'}
  CXXFLAGS=${CXXFLAGS//'-flto=thin'/'-flto'}
  LDFLAGS=${LDFLAGS//'-flto=thin'/'-flto'}
  
  ./bootstrap --no-git --gnulib-srcdir="${srcdir}/gnulib/"
  
  ./configure --prefix='/usr' \
              --sysconfdir='/etc' \
              --mandir='/usr/share/man' \
              --infodir='/usr/share/info' \
              --disable-nls \
              --disable-libcap \
              --without-gmp \
              --enable-no-install-program=$(busybox --list | tr '\n' ',' | sed 's/.$//')
  
  make
}

package() {
  package-init
  
  cd "${pkgbase}"

  make DESTDIR="${tmpdir}" install
  
  rm "${tmpdir}/usr/lib/charset.alias"
}

package_coreutils-doc() {
  package-doc
}

package_join() {
  depends=('musl' 'libattr')
  
  install -d "${pkgdir}/usr/bin/"
  
  mv "${tmpdir}/usr/bin/join" "${pkgdir}/usr/bin/"
}

package_coreutils() {
  package-base
}
