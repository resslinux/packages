pkgbase='musl'
pkgname=('musl-dev' 'musl')
pkgver=1.1.18.r57.g628cf979
pkgrel=1
pkgdesc='Lightweight implementation of C standard library'
url="https://www.musl-libc.org/"
arch=('x86_64')
license=('MIT')
depends_dev=('musl')
depends=('filesystem')
source=('git://git.musl-libc.org/musl'
        '__stack_chk_fail_local.c')
sha256sums=('SKIP'
            '299a7d75a09de3e2e11e7fb4acc3182e4a14e868093d2f30938fce9bfcff13da')

pkgver() {
  cd "${pkgbase}"
  
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgbase}"
  
  # provide minimal libssp_nonshared.a so we don't need libssp from gcc
  ${CROSS_COMPILE}clang $CPPFLAGS $CFLAGS -c "${srcdir}/__stack_chk_fail_local.c" -o "__stack_chk_fail_local.o"
  ${CROSS_COMPILE}ar r "libssp_nonshared.a" "__stack_chk_fail_local.o"

  # note: not autotools
  ./configure --prefix=/lib/musl

  make
}

package() {
  cd "${pkgbase}"
  
  make DESTDIR="${tmpdir}" install

  cp "libssp_nonshared.a" "${tmpdir}/lib/musl/lib/"
  
  install -d "${tmpdir}/bin"
  ln -sf "/lib/ld-musl-${CARCH}.so.1" "${tmpdir}/bin/ldd"

  # remove libintl.h, currently we don't want by default any NLS
  # and use GNU gettext where needed. the plan is to migrate to
  # musl gettext() later on as fully as possible.
  rm "${tmpdir}/lib/musl/include/libintl.h"
}

package_musl-dev() {
  package-dev
}

package_musl() {
  package-base
}
